import com.android.builder.internal.ClassFieldImpl
import com.buildsrc.kts.AndroidConfig
import com.buildsrc.kts.Dependencies

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {

    compileSdkVersion AndroidConfig.compileSdkVersion

    defaultConfig {
        minSdkVersion AndroidConfig.minSdkVersion
        targetSdkVersion AndroidConfig.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            //增加静态值BUILD_TIME
            buildConfigField("long", "BUILD_TIME", "${System.currentTimeMillis()}L")
        }

        debug {
            def t = 3_600_000L//开发模式减少变更，加快运行速度
            buildConfigField("long", "BUILD_TIME", "${((System.currentTimeMillis() / t).longValue() * t).longValue()}L")
        }

        debugProguard {
            initWith(debug)
            //buildConfigField方法每次build会多一个替换的提示
            addBuildConfigField(new ClassFieldImpl("long", "BUILD_TIME", "${System.currentTimeMillis()}L"))
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    viewBinding {
        enabled = true
    }
    kotlinOptions {
        jvmTarget = '1.8'
        //为java继承的kotlin interface default方法添加实现
        freeCompilerArgs += ["-Xjvm-default=all-compatibility"]
    }


}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes' // 动态版本
    resolutionStrategy.cacheChangingModulesFor 10, 'minutes' // 变化模块
}
dependencies {
    api Dependencies.Kotlin.kotlin_stdlib
    api Dependencies.AndroidX.core_ktx
    api Dependencies.AndroidX.appcompat
    api Dependencies.OpenSourceLibrary.unpeekLiveData
    api Dependencies.Foundation.activityFragment
    api Dependencies.Foundation.net
    api Dependencies.Foundation.loadingView
}